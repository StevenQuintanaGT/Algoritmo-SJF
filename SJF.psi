	
    Definir n, tiempoActual, tiempoEsperaTotal, tiempoRetornoTotal, TME, TMR Como Real
    tiempoActual = 0
    tiempoEsperaTotal = 0
    tiempoRetornoTotal = 0
    
    Escribir "Ingrese el número de procesos: "
    Leer n
    
    Dimension id[n], rafagaCPU[n], tiempoLlegada[n], tiempoEspera[n], tiempoRetorno[n]
    Dimension ordenados[n] // Array para almacenar el índice original
	
    // Ingresar detalles de cada proceso
    Para i=0 Hasta n-1 Hacer
        Escribir "Proceso ", i+1, ":"
        Escribir "Ráfaga de CPU: "
        Leer rafagaCPU[i]
        Escribir "Tiempo de llegada: "
        Leer tiempoLlegada[i]
        id[i] = i + 1 // Asignar el id del proceso correspondiente
        ordenados[i] = i // Inicializa el índice original
    FinPara
    
    // Ordenar procesos por tiempo de llegada y luego por ráfaga de CPU
    Para i=0 Hasta n-2 Hacer
        Para j=i+1 Hasta n-1 Hacer
            Si tiempoLlegada[ordenados[i]] > tiempoLlegada[ordenados[j]] O (tiempoLlegada[ordenados[i]] = tiempoLlegada[ordenados[j]] Y rafagaCPU[ordenados[i]] > rafagaCPU[ordenados[j]]) Entonces
                // Intercambiar los índices
                Aux = ordenados[i]
                ordenados[i] = ordenados[j]
                ordenados[j] = Aux
            FinSi
        FinPara
    FinPara
    
    // Calcular tiempos de espera y retorno
    Para i=0 Hasta n-1 Hacer
        Indice = ordenados[i] // Obtener el índice original del proceso
        Si tiempoActual < tiempoLlegada[Indice] Entonces
            tiempoActual = tiempoLlegada[Indice]
        FinSi
        tiempoEspera[Indice] = tiempoActual - tiempoLlegada[Indice]
        tiempoEsperaTotal = tiempoEsperaTotal + tiempoEspera[Indice]
        
        tiempoActual = tiempoActual + rafagaCPU[Indice]
        tiempoRetorno[Indice] = tiempoActual
        tiempoRetornoTotal = tiempoRetornoTotal + tiempoRetorno[Indice]
    FinPara
    
    // Mostrar resultados en el orden original (P1 a P5)
    Escribir "Proceso  Ráfaga  Llegada  Espera  Retorno"
    Para i=0 Hasta n-1 Hacer
        Escribir "P", id[i], "  ", rafagaCPU[i], "  ", tiempoLlegada[i], "  ", tiempoEspera[i], "  ", tiempoRetorno[i]
    FinPara
    
    // Calcular y mostrar TME y TMR
    TME = tiempoEsperaTotal / n
    TMR = tiempoRetornoTotal / n
    Escribir "\nTiempo Medio de Espera (TME): ", TME
    Escribir "Tiempo Medio de Retorno (TMR): ", TMR
	
FinAlgoritmo
